!--------------------------------------------------
! execute MAD-X particle tracking with PTC via this file
! to provide correct input files for libpalattice
!--------------------------------------------------


! insert your MAD-X file here (can be set autom. by libpalattice)
call, file = "test.madx";



! twiss module (must run before ptc):
!-------------------------------------------------
select, flag=error, clear=true;
select, flag=error, class=sbend;
select, flag=error, class=rbend;
esave, file="madx.dipealign";
select, flag=error, clear=true;
select, flag=error, class=quadrupole;
esave, file="madx.quadealign";

! twiss output:
select, flag=twiss, column=keyword, name, s, x, y, l, angle, k1l, k2l, vkick, hkick, e1, e2, apertype, aper_1, aper_2, betx,alfx,mux,dx,dpx,bety,alfy,muy;
twiss, chrom, file="madx.twiss";
!-------------------------------------------------



ptc_create_universe;
ptc_create_layout,model=2,method=6,nst=10,exact;
ptc_align;


! start coordinates for each particle (change with closed_orbit option!)
! * if closed_orbit, coordinates on emittance ellipse can be used WITHOUT any offset *
!--------------------------------------------------------------------------------------
! e.g.:
 ptc_start, x=0.001, px=0.0, y=0.001, py=0.0;
!--------------------------------------------------------------------------------------


! observation points (additional to start):
! needed for libpalattice single particle trajectories
!-----------------------------------------------------------
! set observation points at all lattice elements listed in madx.observe
! madx.observe can be generated by libpalattice including all elements with "BPM" in their name
! (libpalattice calls madx twice: 1. create madx.twiss 2. grep for "BPM" elements & write them to madx.observe)
call,file=madx.observe;



! tracking settings & execution
!--------------------------------------
ptc_track, file = madx,  			    //filenames
	icase = 6,
	deltap = 0.0,
       	element_by_element,			    //small effect on numerical results
       	closed_orbit,                               //coordinates relative to closed orbit
       	//radiation_model1, radiation_energy_loss,  //synchrotron radiation
       	maxaper = 0.1,0.005,0.05,0.005,0.01,0.005,  //aperture	 
       	turns = 1,                                    
	ffile = 1;			            //output after each [ffile] turns
!--------------------------------------

ptc_track_end; 
ptc_end;
stop;